import com.android.build.gradle.tasks.MergeResources

ext {
    minSdkVersion = 8
    targetSdkVersion = 22
    versionCode = 1

    commonVersions = [
            grooidVersion: '2.4.3',
            androidSupportVersion: '22.2.0',
            buildToolsVersion: '22.0.1',
            compileSdkVersion: 22
    ]
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.6'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    }
}

apply plugin: 'org.asciidoctor.gradle.asciidoctor'

allprojects {
    apply plugin: 'idea'
    repositories {
        jcenter()
    }

    version = "1.2.2"
    group = 'com.github.s0nerik.betterknife'
}

gradle.taskGraph.whenReady {
    allprojects {
        tasks.withType(GroovyCompile) { task ->
            logger.lifecycle("Task $task")
            task.groovyOptions.forkOptions.jvmArgs = ['-noverify']
        }
        tasks.withType(MergeResources) { task ->
            task.process9Patch = false
        }
    }
}

tasks.whenTaskAdded {
    if (it.name.contains('lint') || it.name.contains('Lint')) {
        it.enabled = false
    }
}

asciidoctor {
    sourceDir = 'docs' as  File
    options = [
        attributes: [
            'imagesdir':'images',
            'icons': 'font',
            'source-highlighter': 'prettify',
            'toc': 'left'
        ]
    ]
}
