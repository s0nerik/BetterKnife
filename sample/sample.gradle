import com.android.build.gradle.tasks.MergeResources

gradle.taskGraph.whenReady {
    allprojects {
        tasks.withType(GroovyCompile) { task ->
            logger.lifecycle("Task $task")
            task.groovyOptions.forkOptions.jvmArgs = ['-noverify']
        }
        tasks.withType(MergeResources) { task ->
            task.process9Patch = false
        }
    }
}

tasks.whenTaskAdded {
    if (it.name.contains('lint') || it.name.contains('Lint')) {
        it.enabled = false
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'

android {
    compileSdkVersion commonVersions.compileSdkVersion
    buildToolsVersion commonVersions.buildToolsVersion

    defaultConfig {
        applicationId "com.github.s0nerik.betterknife.sample"
        minSdkVersion 15
        targetSdkVersion project.targetSdkVersion

        versionCode project.versionCode
        versionName project.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'LICENSE.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':BetterKnife')
    compile "org.codehaus.groovy:groovy:$commonVersions.grooidVersion:grooid"
    compile "com.android.support:appcompat-v7:$commonVersions.androidSupportVersion"
    compile "com.android.support:support-v4:$commonVersions.androidSupportVersion"



    // Instrumentation testing
    androidTestCompile "com.andrewreitz:spock-android:1.2.1"
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'

    androidTestCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'junit'
    }

    // Optional, needed for mocking
    androidTestCompile "com.google.dexmaker:dexmaker:1.2"



    // Unit testing
    testCompile('org.robospock:robospock:0.6.0-rc') {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'org.spockframework'
        exclude group: 'com.android.support'
    }

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }

    testCompile 'org.mockito:mockito-all:1.9.5'
}
